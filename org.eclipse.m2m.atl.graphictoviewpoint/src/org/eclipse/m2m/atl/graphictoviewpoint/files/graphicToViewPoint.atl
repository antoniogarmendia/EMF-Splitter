-- @nsURI graphicR=http://mondo.org/graphic_representation/1.0.3
-- @nsURI diagram=http://mondo.import.mm.sirius
-- @nsURI table = http://www.eclipse.org/sirius/table/description/1.1.0

module graphicToViewPoint;
create OUT : diagram from IN : graphicR;


rule GraphicRepresentationToGoup {
	from g: graphicR!GraphicRepresentation
	to o: diagram!Group(
		name <- g.name,
		ownedViewpoints <- g.allGraphicRepresentation,
		userColorsPalettes <- thisModule.CreateUserPalette(g)
	)
}

lazy rule CreateUserPalette{
	from g:graphicR!GraphicRepresentation
	to o: diagram!UserColorsPalette
	(
		name <- 'DefaultPalette',
		entries <- g.userColors
	)
}

rule MMGraphicRepresentationTo{
	from mm:graphicR!MMGraphic_Representation
	to o: diagram!Viewpoint(
			name <- 'Vp'.concat(mm.extension),		
			modelFileExtension <- mm.extension,
			ownedRepresentations <- mm.listRepresentations
		)	
}

rule RepresentationDD{
	from mm:graphicR!RepresentationDD
	to o: diagram!DiagramDescription(
			name <- 'Vp'.concat(mm.root.anEClass.name),	
			initialisation <- true,
			label <- mm.root.anEClass.name,
			domainClass <- mm.root.anEClass.ePackage.name.concat('.').concat(mm.root.anEClass.name),
			defaultLayer <- mm.layers->select(dl | dl.isDefaultLayer()),
			additionalLayers <- mm.layers->select(dl | not dl.isDefaultLayer()), 
			metamodel <- mm.root.anEClass.getEPackage()
		)	
}

rule CreateDefaultLayer{
	from
		dl:graphicR!DefaultLayer
	to 
		layer : diagram!Layer (
			name <- dl.name,
			label <- dl.name,
			nodeMappings <- dl.elements->select(e | e.isNode())->select(f | not f.isContainer()),
			edgeMappings <- dl.elements->
							select(n | n.AbstractElementOrNode())->select(e | e.node_elements->oclIsUndefined() = false)
							->select(e | e.node_elements.linkPalette->oclIsUndefined() = false) 
							->select(e | e.node_elements.linkPalette->size() > 0)->collect(f | f.node_elements.linkPalette)
							->flatten()->union(  
							dl.elements->select(f | f.isEdge())	
							),			
			containerMappings <- dl.elements->select(e | e.isNode())->select(f | f.isContainer()),
			toolSections <- thisModule.CreateToolSection(dl)
			)			
}

rule CreateAdditionalLayer{
	from
		dl:graphicR!AdditionalLayer
	to 
		layer : diagram!AdditionalLayer (
			name <- dl.name,
			label <- dl.name,
			nodeMappings <- dl.elements->select(e | e.isNode())->select(f | not f.isContainer()),
			edgeMappings <- dl.elements->
							select(n | n.isNode())->select(e | e.node_elements.linkPalette->oclIsUndefined() = false)
							->select(e | e.node_elements.linkPalette->size() > 0)->collect(f | f.node_elements.linkPalette)
							->flatten()->union(  
							dl.elements->select(f | f.isEdge())	
							),			
			containerMappings <- dl.elements->select(e | e.isNode())->select(f | f.isContainer()),
			toolSections <- thisModule.CreateToolSection(dl)
			)			
}

--Method for Create the Palette Section
lazy rule CreateToolSection{
	from 
		dl:graphicR!Layer
	to 
		ownedRepresentation:diagram!ToolSection(
			name <- 'DefaultPalette',
			ownedTools <- dl.elements->
						  select(n | n.AbstractElementOrNode())->select(e | e.node_elements->oclIsUndefined() = false)
						  ->select(f | f.node_elements.linkPalette->oclIsUndefined() = false)
						  ->select(e | e.node_elements.linkPalette->size() > 0)->collect(f | f.node_elements.linkPalette)
						  ->flatten()->collect(g | thisModule.resolveTemp(g, 'CreateSection'))
						  ->union
						  (dl.elements->collect(d | thisModule.resolveTemp(d, 'CreateSection')))											
		)
}

--Node Mapping & Node with Affixed
rule CreateNodeMapping {
	from
		diagElement:graphicR!Node(not diagElement.isContainer())
	to 
		CreationNodeMapping : diagram!NodeMapping (
			name <- 'Id'.concat(diagElement.anEClass.name),
			domainClass <-  diagElement.refImmediateComposite().refImmediateComposite().root.
							anEClass.ePackage.name.concat('.').concat(diagElement.anEClass.name),														
			semanticCandidatesExpression <- if(diagElement.containerReference->oclIsUndefined()) then 
												'[self/]'
													else if (diagElement.containerReference->size() = 1) then 
													  'feature:'.concat(diagElement.containerReference->asSequence()->first().name)
											  else
												  	thisModule.getSemanticExpressionUsingContainers(diagElement.containerReference->asSequence())
													endif
											endif,
			style <- diagElement.node_shape,
			conditionnalStyles <- diagElement.conditionalStyle->collect(a | thisModule.CreateConditionalNodeStyle(a)),
			reusedBorderedNodeMappings <-  if(diagElement.node_elements->oclIsUndefined()) then
													OclUndefined
												else
													diagElement.node_elements.affixedCompartmentElements->select(e | e.isAffixed())
													->select(f | f.anEReference.isContainment() = true)
													->collect(f | f.nodes)->flatten()										
												endif,
			borderedNodeMappings <- if(diagElement.node_elements->oclIsUndefined()) then
												OclUndefined
											else
												diagElement.node_elements.affixedCompartmentElements->select(e | e.isAffixed())
												->select(f | f.anEReference.isContainment() = false)
												->collect(a | a.nodes->collect(n | thisModule.LazyCreateNodeMapping(Tuple{diagElement = n, diagRef = a.anEReference.name})))												
											endif			
		),
		CreateSection : diagram!NodeCreationDescription (
			name <- 'IdCreation'.concat(diagElement.anEClass.name),
			nodeMappings <- CreationNodeMapping,
			label <-  if(diagElement.diag_palette->oclIsUndefined())
						then 
							''
							else
								diagElement.diag_palette.palette_name
						endif,
			iconPath <- if(diagElement.diag_palette->oclIsUndefined() = true)
						then 
							''
							else
								diagElement.diag_palette.icon_filepath
						endif,
			initialOperation <- InitialOperation
		),
		InitialOperation : diagram!InitialNodeCreationOperation(
			firstModelOperations <- if(diagElement.containerReference->oclIsUndefined() = true) then 
											OclUndefined
										else if (diagElement.containerReference->asSequence()->size() = 1) then
												thisModule.CreateFirstOperation(diagElement)
												else
													thisModule.SwitchOperation(diagElement)
												endif
										endif								
		)		
}

--Create Switch Case
lazy rule SwitchOperation {
	
	from d : graphicR!Node
	to e : diagram!Switch (
		cases <- d.containerReference->collect(g | thisModule.CreateSwitchCase(g))	
	)
}

--Conditional
lazy rule CreateSwitchCase {
	
	from d : Ecore!EReference
	to e: diagram!Case (
		conditionExpression <- 'aql:container.oclIsKindOf(' + d.eContainer().getEPackage().name + '::' + d.eContainer().name +')',	
		subModelOperations <- thisModule.CreateInstance(d)
	)
}

--Create Instance
lazy rule CreateInstance {
	
	from d : Ecore!EReference
	to e : diagram!CreateInstance (
		referenceName <- d.name,
		typeName <- d.eContainer().getEPackage().name + '.' + d.getEType().name					 	
	)
}

--Container Mapping
rule CreateContainerMapping{
	from 
		diagElement:graphicR!Node(diagElement.isContainer())
	to 
			CreateContainer : diagram!ContainerMapping(
				name <- 'Id'.concat(if(diagElement.anEClass->oclIsUndefined() = true) then
										''
									     else
												diagElement.anEClass.name
										endif			
									),
				domainClass <- diagElement.refImmediateComposite().refImmediateComposite().root.
								anEClass.ePackage.name.concat('.').concat(if(diagElement.anEClass->oclIsUndefined() = true) then
										''
									     else
												diagElement.anEClass.name
										endif),
				semanticCandidatesExpression <- if(diagElement.containerReference->oclIsUndefined() = true) then 
													''
													   else	if(diagElement.containerReference->size() = 1) then			
															'feature:'.concat(diagElement.containerReference->asSequence()->first().name) 
															else
																thisModule.getSemanticExpressionUsingContainers(diagElement.containerReference->asSequence())
														endif
												endif,
				style <- if (diagElement.node_shape->oclIsUndefined() = true) then 
									Sequence{}
							else
									diagElement.node_shape
								endif,
				conditionnalStyles <- diagElement.conditionalStyle->collect(a | thisModule.CreateConditionalContainerStyle(a)),	
				childrenPresentation <- thisModule.getChildrenPresentation(diagElement.presentation),
				borderedNodeMappings <- if(diagElement.node_elements->oclIsUndefined()) then
											OclUndefined
										else
											if (diagElement.node_elements.affixedCompartmentElements->oclIsUndefined() = false)	then
												diagElement.node_elements.affixedCompartmentElements->select(e | e.isAffixed())->
														select(f | f.nodes->oclIsUndefined() = false)->flatten()
														->collect(e | e.nodes)->flatten()->select(g | not g.isContainer())
													else
												Sequence{}	
											endif
										endif,
				
				reusedContainerMappings <- if(diagElement.node_elements->oclIsUndefined()) then
													OclUndefined
												else
													if (diagElement.node_elements.affixedCompartmentElements->oclIsUndefined() = false)	then
														diagElement.node_elements.affixedCompartmentElements->select(e | e.isCompartment())->
																select(f | f.nodes->oclIsUndefined() = false)->flatten()
																->collect(e | e.nodes)->flatten()->select(g | g.isContainer())-->select(h | h.equals(diagElement))
															else
														Sequence{}	
													endif
												endif,
				reusedNodeMappings <- if(diagElement.node_elements->oclIsUndefined()) then
										OclUndefined
									else
										if (diagElement.node_elements.affixedCompartmentElements->oclIsUndefined() = false)	then
											diagElement.node_elements.affixedCompartmentElements->select(e | e.isCompartment())->
													select(f | f.nodes->oclIsUndefined() = false)->flatten()
													->collect(e | e.nodes)->flatten()->select(g | not g.isContainer())
											else
											Sequence{}	
										endif
									endif,
				reusedBorderedNodeMappings <-  if(diagElement.node_elements->oclIsUndefined()) then
													OclUndefined
												else
													diagElement.node_elements.affixedCompartmentElements->select(e | e.isAffixed())
													->select(f | f.anEReference.isContainment() = true)
													->collect(f | f.nodes)->flatten()										
												endif												
			),
			CreateSection : diagram!ContainerCreationDescription (
			name <- 'IdCreation'.concat(if(diagElement.anEClass->oclIsUndefined() = true) then
											''
									     else
											diagElement.anEClass.name
										endif),
			containerMappings <- CreateContainer,
			label <- if(diagElement.diag_palette->oclIsUndefined())
						then 
							''
							else
								diagElement.diag_palette.palette_name
						endif
						,
			iconPath <- if(diagElement.diag_palette->oclIsUndefined())
						then 
							''
							else
								diagElement.diag_palette.icon_filepath
						endif,
			initialOperation <- InitialOperation
			),
			InitialOperation : diagram!InitialNodeCreationOperation(
				firstModelOperations <- if (diagElement.containerReference->oclIsUndefined() = true) then 
											OclUndefined
												else if (diagElement.containerReference->asSequence()->size() = 1) then												
														thisModule.CreateFirstOperation(diagElement)
													else
														thisModule.SwitchOperation(diagElement)
														endif
													endif
			)			
}

lazy rule CreateFirstOperation {
	
	from d: graphicR!Node
	to e: diagram!CreateInstance(
				referenceName <- d.containerReference->asSequence()->first().name,										
				typeName <- d.refImmediateComposite().refImmediateComposite().root.
							anEClass.ePackage.name.concat('.').concat(d.anEClass.name)						
	)			

}

lazy rule CreateConditionalContainerStyle {
	
	from d: graphicR!ConditionalStyle
	to e: diagram!ConditionalContainerStyleDescription(
		
		predicateExpression <- 'aql:self.'+ d.eAttribute.name.toString() + '.toString()=\'' +  d.value.toString() + '\'',
		style <- d.conditionalStyle
	)
}

lazy rule CreateConditionalNodeStyle {
	
	from d: graphicR!ConditionalStyle
	to e: diagram!ConditionalNodeStyleDescription(
		
		predicateExpression <- 'aql:self.'+ d.eAttribute.name.toString() + '.toString()=\'' +  d.value.toString() + '\'',
		style <- d.conditionalStyle
	)
}

rule CreateParalellogram{
	
		from d: graphicR!ShapeCompartmentParallelogram
		to e: diagram!ShapeContainerStyleDescription(
			labelExpression <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
									''
									else
										thisModule.LabelExpression(d.refImmediateComposite().node_elements.LabelanEAttribute)										
										endif,
			widthComputationExpression <- d.width.toString(),
			heightComputationExpression <- d.height.toString(),
			backgroundColor <- if(d.color->oclIsUndefined() = true) then
									OclUndefined
									else if(d.color.isSiriusSystemColors() = true) then
										d.color.getSystemColorDescription(d.color.name)
										else
											d.color.color
											endif
								endif,
			showIcon <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
							  false
							else
							if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon->oclIsUndefined() = true) then
									false
									else
										d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon
							endif
						endif
		)	
}

--Edge Mapping
rule CreateEdgeMapping {
		from
			diagElement:graphicR!Edge
		to 
			e : diagram!EdgeMapping (
				name <- 'Id'.concat(diagElement.anEClass.name),
				domainClass <- diagElement.refImmediateComposite().refImmediateComposite().root.
							anEClass.ePackage.name.concat('.').concat(diagElement.anEClass.name),
				useDomainElement <- true,
				--semanticCandidatesExpression <- '['.concat(if (diagElement.containerReference->oclIsUndefined())
					--											then
						--											''
							--										else
								--								diagElement.containerReference.name
									--							endif
										--					 ).concat('/]'),
				sourceFinderExpression <- '[self.'.concat(diagElement.directions.sourceLink.anEReference.name).concat('/]'),
				targetFinderExpression <- '[self.'.concat(diagElement.directions.targetLink.anEReference.name).concat('/]'),						
				sourceMapping <- diagElement.directions.sourceLink.anDiagramElement,
				targetMapping <- diagElement.directions.targetLink.anDiagramElement,
				style <- thisModule.CreateDefaultStyle(diagElement)
			),
			CreateSection : diagram!EdgeCreationDescription(
				name <- 'IdCreation'.concat(diagElement.anEClass.name),
				edgeMappings <- e,
				label <- if(diagElement.diag_palette->oclIsUndefined())
						then 
							''
							else
								diagElement.diag_palette.palette_name
						endif,
				iconPath <- if(diagElement.diag_palette->oclIsUndefined())
						then 
							''
							else
								diagElement.diag_palette.icon_filepath
						endif,
				initialOperation <- InitialOperation
			),
			InitialOperation : diagram!InitEdgeCreationOperation(
				firstModelOperations <- if(diagElement.containerReference->oclIsUndefined()) then
											''
											else if (diagElement.containerReference->asSequence()->size() = 1) then
												FirstEdgeOperation
												else
													''--The edge is contained by two EClass TODO
													endif
											endif
			),
			FirstEdgeOperation : diagram!ChangeContext(
				browseExpression <- '[source.eContainer("'.concat(diagElement.containerReference->asSequence()->first().eContainingClass.name).concat('")/]'),
				subModelOperations <- OperationInstance				
			),
			OperationInstance : diagram!CreateInstance(
				typeName <- diagElement.refImmediateComposite().refImmediateComposite().root.
							anEClass.ePackage.name.concat('.').concat(diagElement.anEClass.name),
				referenceName <- diagElement.containerReference->asSequence()->first().name,
				subModelOperations <- SubModelOperation
			),
			SubModelOperation : diagram!SetValue(
				featureName <- diagElement.directions.sourceLink.anEReference.name,
				valueExpression <- 'var:source',--.concat(diagElement.directions.sourceLink.anEReference.name),
				subModelOperations <- SubSubModelOperation
			),
			SubSubModelOperation : diagram!SetValue(
				featureName <- diagElement.directions.targetLink.anEReference.name,
				valueExpression <- 'var:target'--.concat(diagElement.directions.targetLink.anEReference.name)
			)
}

--Create Relation-Based Edge
rule CreateRelationalEdge{
	
	from 
		linkPalette:graphicR!PaletteDescriptionLink
	to
		e: diagram!EdgeMapping(
			
			name <- 'Id'.concat(linkPalette.refImmediateComposite().refImmediateComposite().anEClass.name).concat(linkPalette.anEReference.eType.name).concat(linkPalette.anEReference.name),
			sourceMapping <- if linkPalette.refImmediateComposite().refImmediateComposite()->oclIsTypeOf(graphicR!AbstractElement) = true then
										thisModule.geteAllChildren(linkPalette.refImmediateComposite().refImmediateComposite().refImmediateComposite(),
														linkPalette.refImmediateComposite().refImmediateComposite())
										else
										 linkPalette.refImmediateComposite().refImmediateComposite()
										endif,
			targetMapping <-  if linkPalette.refImmediateComposite().refImmediateComposite()->oclIsTypeOf(graphicR!AbstractElement) = true then	
								  thisModule.geteAllChildrenEClass(linkPalette.refImmediateComposite().refImmediateComposite().refImmediateComposite(),
														linkPalette.anEReference.getEType())	
								else
									linkPalette.anDiagramElement
								endif,
			useDomainElement <- false,
			targetFinderExpression <- if(linkPalette.anEReference.name->oclIsUndefined() = true) then
											OclUndefined
											else
												'feature:'.concat(linkPalette.anEReference.name)
												endif,
			style <- thisModule.CreateRelationBasedStyle(linkPalette)
		),
		CreateSection : diagram!EdgeCreationDescription(
				name <- 'IdCreation'.concat(linkPalette.refImmediateComposite().refImmediateComposite().anEClass.name).concat(linkPalette.anEReference.eType.name).concat(linkPalette.anEReference.name),
				edgeMappings <- e,
				label <- if(linkPalette.palette_name->oclIsUndefined())
						then 
							''
							else
								linkPalette.palette_name
						endif,
				iconPath <- if(linkPalette.icon_filepath->oclIsUndefined())
						then 
							''
							else
								linkPalette.icon_filepath
						endif,
				initialOperation <- InitialOperation
			),
			InitialOperation : diagram!InitEdgeCreationOperation(
				firstModelOperations <- SubModelOperation	
			),
			SubModelOperation : diagram!SetValue(
				featureName <- linkPalette.anEReference.name,
				valueExpression <- 'var:target'				
			)
			
}

--Create Relation Based Style
lazy rule CreateRelationBasedStyle{
	from  linkPalette:graphicR!PaletteDescriptionLink
	to f: diagram!EdgeStyleDescription(
		targetArrow <- linkPalette.decoratorName,
		sourceArrow <- linkPalette.sourceDecoratorName,	
		strokeColor <- if(linkPalette.color->oclIsUndefined() = true) then
								OclUndefined
							else if(linkPalette.color.isSiriusSystemColors() = true) then 
									linkPalette.color.getSystemColorDescription(linkPalette.color.name)
								else
									linkPalette.color.color
								endif
							endif,
		lineStyle <- linkPalette.LineStyle,
		centerLabelStyleDescription <- thisModule.CreateCenterLabelStyleDescriptionRelationalEdge(linkPalette)
	)
}

lazy rule CreateCenterLabelStyleDescriptionRelationalEdge{
	from diagElement:graphicR!PaletteDescriptionLink
	to e: diagram!CenterLabelStyleDescription(
			
		)
}

lazy rule CreateDefaultStyle{
		from diagElement:graphicR!Edge
		to e: diagram!EdgeStyleDescription(
			targetArrow <- diagElement.directions.targetLink.decoratorName,
			sourceArrow <- diagElement.directions.sourceLink.decoratorName,
			centerLabelStyleDescription <- thisModule.CreateCenterLabelStyleDescription(diagElement),
			strokeColor <-  if(diagElement.edge_style.color->oclIsUndefined() = true) then
								OclUndefined
							else if(diagElement.edge_style.color.isSiriusSystemColors() = true) then 
									diagElement.edge_style.color.getSystemColorDescription(diagElement.edge_style.color.name)
								else
									diagElement.edge_style.color.color
								endif
							endif,
			lineStyle <- diagElement.edge_style.LineStyle
			)
}

lazy rule CreateCenterLabelStyleDescription{
	from diagElement:graphicR!Edge
	to e: diagram!CenterLabelStyleDescription(
			labelExpression <- if(diagElement.edgeLabel->oclIsUndefined()) then
										'' 
									else
										'feature:'+diagElement.edgeLabel.labelEAttribute.name
								endif,								
			showIcon <- false
		)
}

rule CreateSquare{
		from d:graphicR!Rectangle
		to e: diagram!SquareDescription(
			width <- d.width, 
			height <- d.height,
			labelExpression <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
									''
									else
										thisModule.LabelExpression(d.refImmediateComposite().node_elements.LabelanEAttribute)										
										endif, 
			showIcon <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
								false
								else
									d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon
						endif,
			resizeKind <- 'NSEW',
			color <- if(d.color->oclIsUndefined() = true) then
					 	OclUndefined
						else
							if(d.color.isSiriusSystemColors() = true) then
								d.color.getSystemColorDescription(d.color.name)
							else
								d.color.color
							endif
						endif,			
							
			labelColor <- 	if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
								OclUndefined
								else 
									if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.isSiriusSystemColors() = true) then
										d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.getSystemColorDescription
											(d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.name)
										else
											d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.color
									endif
							endif,				
			
			borderColor <- if(d.borderColor->oclIsUndefined() = true) then
					 		OclUndefined
							else
								if(d.borderColor.isSiriusSystemColors() = true) then
									d.borderColor.getSystemColorDescription(d.borderColor.name)
								else
									d.borderColor.color
								endif
							endif,
			borderLineStyle <- d.borderStyle,
			borderSizeComputationExpression <- d.borderWidth,
			labelFormat <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
								OclUndefined
							else		
								d.refImmediateComposite().node_elements.LabelanEAttribute->first().labelFormat								
							endif,	
			labelPosition <-if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
								OclUndefined
							else		
								d.refImmediateComposite().node_elements.LabelanEAttribute->first().labelPosition								
							endif
		)
}

rule CreateEllipse{
		from d:graphicR!Ellipse
		to e: diagram!EllipseNodeDescription(
			horizontalDiameterComputationExpression <- d.horizontalDiameter.toString(), 
			verticalDiameterComputationExpression <- d.verticalDiameter.toString(),
			labelExpression <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
									''
									else
										thisModule.LabelExpression(d.refImmediateComposite().node_elements.LabelanEAttribute)										
										endif,
			showIcon <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
								false
							else
						if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon->oclIsUndefined() = true) then
								false
								else
									d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon
							endif
						endif,
			resizeKind <- 'NSEW',
			color <- if(d.color->oclIsUndefined() = true) then OclUndefined else
						if(d.color.isSiriusSystemColors() = true) then
								d.color.getSystemColorDescription(d.color.name)
								else
									d.color.color
								endif
						endif,
			labelColor <- 	if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
								OclUndefined
								else
								if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().color->oclIsUndefined() = true) then
									OclUndefined
									else
										if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.isSiriusSystemColors() = true) then
											d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.getSystemColorDescription
												(d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.name)
											else
												d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.color
												endif
								endif
							endif,
			borderColor <- if(d.borderColor.isSiriusSystemColors() = true) then
								d.borderColor.getSystemColorDescription(d.borderColor.name)
							else
								d.borderColor.color
							endif,
			borderLineStyle <- d.borderStyle,
			borderSizeComputationExpression <- d.borderWidth,
			labelFormat <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
								OclUndefined
								else
								if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().labelFormat->oclIsUndefined() = true) then
									OclUndefined
								else		
									d.refImmediateComposite().node_elements.LabelanEAttribute->first().labelFormat								
								endif
							endif,	
			labelPosition <-if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
								OclUndefined
								else
									if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().labelPosition->oclIsUndefined() = true) then
										OclUndefined
									else		
										d.refImmediateComposite().node_elements.LabelanEAttribute->first().labelPosition								
									endif
							endif
		)		
}

rule CreateWorkspaceImage{
		from d:graphicR!IconElement
		to e: diagram!WorkspaceImageDescription(
			workspacePath <- d.filepath,
			resizeKind <- 'NSEW',
			labelExpression <- if(thisModule.getNodeElements(d).node_elements->oclIsUndefined() = true) then
									''
									else 
										if(thisModule.getNodeElements(d).node_elements.LabelanEAttribute->oclIsUndefined() = true) then
											''
											else
											if(thisModule.getNodeElements(d).node_elements.LabelanEAttribute->asSequence()->size() = 0 ) then
													''
													else															
														thisModule.LabelExpression(thisModule.getNodeElements(d).node_elements.LabelanEAttribute)
											endif
										endif
								 endif,										
			showIcon <- if(thisModule.getNodeElements(d).node_elements->oclIsUndefined() = true) then
							false
							else
						if(thisModule.getNodeElements(d).node_elements.LabelanEAttribute->oclIsUndefined() = true) then
							false
							else
						if(thisModule.getNodeElements(d).node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
									false
								else 
									thisModule.getNodeElements(d).node_elements.LabelanEAttribute->first().showIcon
							endif
						endif
						endif
		)
}

rule CreateGradient{
		from d: graphicR!ShapeCompartmentGradient
		to e: diagram!FlatContainerStyleDescription(
			labelExpression <- if(d.refImmediateComposite().node_elements->oclIsUndefined() = true) then
									''
										else 
											if(d.refImmediateComposite().node_elements.LabelanEAttribute->asSequence()->size() = 0 ) then
														''
														else															
															thisModule.LabelExpression(d.refImmediateComposite().node_elements.LabelanEAttribute)
													endif
												endif,
			widthComputationExpression <- d.width.toString(),
			heightComputationExpression <- d.height.toString(),
			backgroundColor <- if(d.color.isSiriusSystemColors() = true) then
									d.color.getSystemColorDescription(d.color.name)
										else
											d.color.color
								endif,
			foregroundColor <- 	if(d.foreGroundColor.isSiriusSystemColors() = true) then
									d.foreGroundColor.getSystemColorDescription(d.color.name)
										else
											d.foreGroundColor.color
								endif,				
			showIcon <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
							false
							else				
							if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon->oclIsUndefined() = true) then
									false
									else
										d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon
							endif
						endif						
		)
}

rule CreateDiamond{
		from d: graphicR!Diamond
		to e: diagram!LozengeNodeDescription(
			labelExpression <- if(d.refImmediateComposite().node_elements->oclIsUndefined() = true) then
									''
										else 
											if(d.refImmediateComposite().node_elements.LabelanEAttribute->asSequence()->size() = 0 ) then
														''
														else															
															thisModule.LabelExpression(d.refImmediateComposite().node_elements.LabelanEAttribute)
													endif
												endif,
			resizeKind <- 'NSEW',
			color <- if(d.color.isSiriusSystemColors() = true) then
									d.color.getSystemColorDescription(d.color.name)
										else
											d.color.color
								endif,
			sizeComputationExpression <- d.size.toString(),
			showIcon <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then 
							OclUndefined
							else
								if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon->oclIsUndefined() = true) then
										false
										else
											d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon
								endif
							endif
		)		
}

rule CreateNote{
		from d: graphicR!Note
		to e: diagram!NoteDescription(
			labelExpression <- if(d.refImmediateComposite().node_elements->oclIsUndefined() = true) then
									''
										else 
											if(d.refImmediateComposite().node_elements.LabelanEAttribute->asSequence()->size() = 0 ) then
														''
														else															
															thisModule.LabelExpression(d.refImmediateComposite().node_elements.LabelanEAttribute)
													endif
												endif,
			resizeKind <- 'NSEW',
			color <- if(d.color.isSiriusSystemColors() = true) then
									d.color.getSystemColorDescription(d.color.name)
										else
											d.color.color
								endif,
			sizeComputationExpression <- d.size.toString(),
			showIcon <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon->oclIsUndefined() = true) then
								false
								else
									d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon
						endif
		)
}

rule CreateRGBColor{
	from d:graphicR!RGBColor
	to e:diagram!ComputedColor(
		name <- d.name,
		red <- d.red.toString(),
		green <- d.green.toString(),
		blue <- d.blue.toString()
	)	
}

helper context graphicR!Layer def: isDefaultLayer(): Boolean =
	if self.oclIsTypeOf(graphicR!DefaultLayer) then
		true
		else
			false
			endif;

helper context graphicR!AllElements def: isNodeOrEdge(): Boolean =
	if self.oclIsTypeOf(graphicR!Node) or self.oclIsTypeOf(graphicR!Edge) then
		true
			else
				false
				endif;

helper context graphicR!AllElements def: isNode(): Boolean =
	if self.oclIsTypeOf(graphicR!Node) then
		true
		else
			false
			endif;

helper context graphicR!AllElements def : AbstractElementOrNode() : Boolean = 
	if self.oclIsTypeOf(graphicR!Node) or self.oclIsTypeOf(graphicR!AbstractElement) then
		true
			else
				false
				endif;

helper context graphicR!AllElements def: isEdge(): Boolean =
	if self.oclIsTypeOf(graphicR!Edge) then
		true
		else
			false
			endif;

helper context graphicR!AffixedCompartmentElement def : isAffixed(): Boolean =
	if self.oclIsTypeOf(graphicR!AffixedElement) then
		true
		else
			false
			endif;

--Identify Sirius Colors
helper context graphicR!Color def : isSiriusSystemColors(): Boolean =
	if self.oclIsTypeOf(graphicR!SiriusSystemColors) then
		true
		else
			false
			endif;

--Identify RGB Colors
helper context graphicR!Color def : isRGBColor(): Boolean =
	if self.oclIsTypeOf(graphicR!RGBColor) then
		true
		else
			false
			endif;


--Indentify Containers
helper context graphicR!Node def: isContainer(): Boolean =
	if self.node_elements->oclIsUndefined() = false
		then 
			if self.node_elements.affixedCompartmentElements->select(e | e.isCompartment() or e.isAffixed())->asSequence()->size() > 0 then
				true
			else
				false
		endif
	else
		false	
	endif;

--Identify Compartments
helper context graphicR!AffixedCompartmentElement def: isCompartment() : Boolean =
	if self.oclIsTypeOf(graphicR!CompartmentElement) then
		true
		else
			false
			endif;

--Label Expression 
helper def : LabelExpression(s : Sequence(graphicR!LabelEAttribute)) : String = 
		if s->size() = 1 then
			'feature:' + s->first().anEAttribute.name
			else
				s->iterate(e; acc: String = 'aql:self.' | 
				if acc = 'aql:self.' then
					acc + e.anEAttribute.name
					else
					acc + '+ \' \' + self.' + e.anEAttribute.name	
				endif)
		endif;

--Find all classes that inherit from an AbstractElement
helper def : geteAllChildren(layer : graphicR!Layer, abstractElement : graphicR!AbstractElement) : Sequence(graphicR!DiagramElement) = 
	layer.elements->select(e | e.isNodeOrEdge())->select(f | abstractElement.anEClass.isSuperTypeOf(f.anEClass).booleanValue());
										
--Find all classes that inherit from an AbstractElement
helper def : geteAllChildrenEClass(layer : graphicR!Layer, anEClass : MMEcore!EClass) : Sequence(graphicR!DiagramElement) = 
	layer.elements->select(e | e.isNodeOrEdge())->select(f | anEClass.isSuperTypeOf(f.anEClass).booleanValue());

--Find node_elements
helper def : getNodeElements(icon : graphicR!IconElement) : graphicR!Node =
	if(icon.refImmediateComposite().oclIsKindOf(graphicR!Node)) then
			icon.refImmediateComposite()
		else
			icon.refImmediateComposite().refImmediateComposite()
			endif;		

--Children Presentation
helper def: getChildrenPresentation(presentation : graphicR!ChildrenPresentation) : String =
	if(presentation.name.toString() = 'FREEFORM')then
		'FreeForm'
		else if (presentation.name.toString() = 'LIST') then
			'List'
			else if (presentation.name.toString() = 'HORIZONTALSTACK') then
				'HorizontalStack'
				else
					'VerticalStack'
			endif
		endif
	endif;

helper def: getSemanticExpressionUsingContainers(s : Sequence(Ecore!EReference)) : String = 
	thisModule.getIfExpressionUsingContainers(s) + ' else self' + thisModule.getEndIfUsingContainers(s);

helper def: getEndIfUsingContainers(s : Sequence(Ecore!EReference)) : String = 
	s->iterate(e; acc: String = '' |
	acc + ' endif'
);

helper def: getIfExpressionUsingContainers(s : Sequence(Ecore!EReference)) : String = 
	s->iterate(e; acc: String = 'aql:if' |
		if (acc = 'aql:if') then 
			acc + '(self.oclIsKindOf('+ e.eContainer().getEPackage().name +'::'+ e.eContainer().name +')) then self.' + e.name
				else
					acc + ' else if ' + '(self.oclIsKindOf('+ e.eContainer().getEPackage().name +'::'+ e.eContainer().name +')) then self.' + e.name
		endif);

--Lazy Rule to create borderedNodes
lazy rule LazyCreateNodeMapping {
	from
		 s :TupleType (diagElement:graphicR!Node, diagRef: String)
	to 
		CreationNodeMapping : diagram!NodeMapping (
			name <- 'Id'.concat(s.diagElement.anEClass.name),
			domainClass <-  s.diagElement.refImmediateComposite().refImmediateComposite().root.
							anEClass.ePackage.name.concat('.').concat(s.diagElement.anEClass.name),														
			semanticCandidatesExpression <- if(s.diagElement.containerReference->oclIsUndefined()) then 
												'[self/]'
													else if (s.diagElement.containerReference->size() = 1) then 
													  'feature:'.concat(s.diagElement.containerReference->asSequence()->first().name)
											  else
												  	thisModule.getSemanticExpressionUsingContainers(s.diagElement.containerReference->asSequence())
													endif
											endif,
			style <- if(s.diagElement.node_shape->oclIsKindOf(graphicR!IconElement)) then
						thisModule->LazyCreateWorkspaceImage(s.diagElement.node_shape) 
						else
							if(s.diagElement.node_shape->oclIsKindOf(graphicR!Rectangle)) then
								thisModule->LazyCreateSquare(s.diagElement.node_shape)
								else
									if(s.diagElement.node_shape->oclIsKindOf(graphicR!Ellipse)) then
										thisModule->LazyCreateEllipse(s.diagElement.node_shape)
										else
											if(s.diagElement.node_shape->oclIsKindOf(graphicR!Note)) then
												thisModule->LazyCreateNote(s.diagElement.node_shape)
												else
													if(s.diagElement.node_shape->oclIsKindOf(graphicR!Diamond)) then
														thisModule->LazyCreateDiamond(s.diagElement.node_shape)
														else
															OclUndefined
														endif
												endif
										endif
								endif
							endif
		)
}

lazy rule LazyCreateWorkspaceImage{
		from d:graphicR!IconElement
		to e: diagram!WorkspaceImageDescription(
			workspacePath <- d.filepath,
			resizeKind <- 'NSEW',
			labelExpression <- if(thisModule.getNodeElements(d).node_elements->oclIsUndefined() = true) then
									''
										else 
											if(thisModule.getNodeElements(d).node_elements.LabelanEAttribute->oclIsUndefined() = true) then
												''
												else
											if(thisModule.getNodeElements(d).node_elements.LabelanEAttribute->asSequence()->size() = 0 ) then
														''
														else															
															thisModule.LabelExpression(thisModule.getNodeElements(d).node_elements.LabelanEAttribute)
													endif
												endif
											 endif,										
			showIcon <- if(thisModule.getNodeElements(d).node_elements->oclIsUndefined() = true) then
							false
							else
						if(thisModule.getNodeElements(d).node_elements.LabelanEAttribute->oclIsUndefined() = true) then
							false
							else
						if(thisModule.getNodeElements(d).node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
									false
								else 
									thisModule.getNodeElements(d).node_elements.LabelanEAttribute->first().showIcon
							endif
						endif
						endif
		)
}

lazy rule LazyCreateSquare{
		from d:graphicR!Rectangle
		to e: diagram!SquareDescription(
			width <- d.width, 
			height <- d.height,
			labelExpression <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
									''
									else
										thisModule.LabelExpression(d.refImmediateComposite().node_elements.LabelanEAttribute)										
										endif, 
			showIcon <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
								false
								else
									d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon
						endif,
			resizeKind <- 'NSEW',
			color <- if(d.color.isSiriusSystemColors() = true) then
								d.color.getSystemColorDescription(d.color.name)
							else
								d.color.color
							endif,			
							
			labelColor <- 	if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
								OclUndefined
								else 
									if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.isSiriusSystemColors() = true) then
										d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.getSystemColorDescription
											(d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.name)
										else
											d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.color
									endif
							endif,				
			
			borderColor <- if(d.borderColor.isSiriusSystemColors() = true) then
								d.borderColor.getSystemColorDescription(d.borderColor.name)
							else
								d.borderColor.color
							endif,
			borderLineStyle <- d.borderStyle,
			borderSizeComputationExpression <- d.borderWidth,
			labelFormat <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
								OclUndefined
							else		
								d.refImmediateComposite().node_elements.LabelanEAttribute->first().labelFormat								
							endif,	
			labelPosition <-if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
								OclUndefined
							else		
								d.refImmediateComposite().node_elements.LabelanEAttribute->first().labelPosition								
							endif
		)
}

lazy rule LazyCreateEllipse{
		from d:graphicR!Ellipse
		to e: diagram!EllipseNodeDescription(
			horizontalDiameterComputationExpression <- d.horizontalDiameter.toString(), 
			verticalDiameterComputationExpression <- d.verticalDiameter.toString(),
			labelExpression <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first()->oclIsUndefined() = true) then
									''
									else
										thisModule.LabelExpression(d.refImmediateComposite().node_elements.LabelanEAttribute)										
										endif,
			showIcon <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon->oclIsUndefined() = true) then
								false
								else
									d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon
						endif,
			resizeKind <- 'NSEW',
			color <- if(d.color.isSiriusSystemColors() = true) then
							d.color.getSystemColorDescription(d.color.name)
							else
								d.color.color
							endif,
			labelColor <- 	if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().color->oclIsUndefined() = true) then
								OclUndefined
								else
									if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.isSiriusSystemColors() = true) then
										d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.getSystemColorDescription
											(d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.name)
										else
											d.refImmediateComposite().node_elements.LabelanEAttribute->first().color.color
											endif
							endif,
			borderColor <- if(d.borderColor.isSiriusSystemColors() = true) then
								d.borderColor.getSystemColorDescription(d.borderColor.name)
							else
								d.borderColor.color
							endif,
			borderLineStyle <- d.borderStyle,
			borderSizeComputationExpression <- d.borderWidth,
			labelFormat <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().labelFormat->oclIsUndefined() = true) then
								OclUndefined
							else		
								d.refImmediateComposite().node_elements.LabelanEAttribute->first().labelFormat								
							endif,	
			labelPosition <-if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().labelPosition->oclIsUndefined() = true) then
								OclUndefined
							else		
								d.refImmediateComposite().node_elements.LabelanEAttribute->first().labelPosition								
							endif
		)		
}

lazy rule LazyCreateDiamond{
		from d: graphicR!Diamond
		to e: diagram!LozengeNodeDescription(
			labelExpression <- if(d.refImmediateComposite().node_elements->oclIsUndefined() = true) then
									''
										else 
											if(d.refImmediateComposite().node_elements.LabelanEAttribute->asSequence()->size() = 0 ) then
														''
														else															
															thisModule.LabelExpression(d.refImmediateComposite().node_elements.LabelanEAttribute)
													endif
												endif,
			resizeKind <- 'NSEW',
			color <- if(d.color.isSiriusSystemColors() = true) then
									d.color.getSystemColorDescription(d.color.name)
										else
											d.color.color
								endif,
			sizeComputationExpression <- d.size.toString(),
			showIcon <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon->oclIsUndefined() = true) then
								false
								else
									d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon
						endif
		)		
}

lazy rule LazyCreateNote{
		from d: graphicR!Note
		to e: diagram!NoteDescription(
			labelExpression <- if(d.refImmediateComposite().node_elements->oclIsUndefined() = true) then
									''
										else 
											if(d.refImmediateComposite().node_elements.LabelanEAttribute->asSequence()->size() = 0 ) then
														''
														else															
															thisModule.LabelExpression(d.refImmediateComposite().node_elements.LabelanEAttribute)
													endif
												endif,
			resizeKind <- 'NSEW',
			color <- if(d.color.isSiriusSystemColors() = true) then
									d.color.getSystemColorDescription(d.color.name)
										else
											d.color.color
								endif,
			sizeComputationExpression <- d.size.toString(),
			showIcon <- if(d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon->oclIsUndefined() = true) then
								false
								else
									d.refImmediateComposite().node_elements.LabelanEAttribute->first().showIcon
						endif
		)
}

--Table Representation

rule RepresentationTable{
	from mm:graphicR!RepresentationTable
	to o:diagram!EditionTableDescription(
		    name <- 'Table '.concat(mm.rootTable.name).concat('[').concat(mm.lines->first().featureReference.name).concat(']'),
			domainClass <- mm.rootTable.ePackage.name.concat('.').concat(mm.rootTable.name),
			initialisation <- true,
			ownedLineMappings <- mm.lines->collect(l | l.listEClasses->collect(c | thisModule.LazyTableLineGroup(Tuple{mm = c, diagRef = l.featureReference.name}))),
			ownedColumnMappings <- mm.columns->collect(attr | thisModule.LazyTableColumns(attr))
		)	
}

lazy rule LazyTableLineGroup{
	from  s :TupleType (mm:MMEcore!EClass, diagRef: String)
	to o:diagram!LineMapping(
		name <- s.mm.name.concat('Line'),
		domainClass <- s.mm.ePackage.name.concat('.').concat(s.mm.name),
		semanticCandidatesExpression <- '[self.'.concat(s.diagRef).concat('/]')
	)	
}

lazy rule LazyTableColumns{
	from mm:MMEcore!EAttribute
	to o:diagram!FeatureColumnMapping(
		name <- mm.name.concat('Column'),
		featureName <- mm.name,
		headerLabelExpression <- mm.name
	)	
}
